buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
  }
}

plugins {
  id "com.gradle.build-scan" version "1.9"
  id "org.jetbrains.kotlin.jvm" version "1.1.51"
  id 'com.github.ben-manes.versions' version '0.15.0'
  id 'net.nemerosa.versioning' version '2.6.1'
  id 'io.codearte.nexus-staging' version '0.11.0'
}

ext.junit4Version = '4.12'
ext.junitVintageVersion = '4.12.0'
ext.junitPlatformVersion = '1.0.0'
ext.junitJupiterVersion = '5.0.0'
ext.kotlin_version = '1.1.51'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'signing'

group = 'com.baulsupp'
archivesBaseName = "oksocial-output"

versioning {
  // TODO automate this
  releaseMode = project.hasProperty('releaseMode') ? project.property('releaseMode') : 'snapshot'
}

version = versioning.info.display

description = "OkHttp Social Output"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

repositories {
  jcenter()
  maven { url "https://jitpack.io" }
  maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.51"
  implementation "org.jetbrains.kotlin:kotlin-reflect:1.1.51"

  compile group: 'org.zeroturnaround', name: 'zt-exec', version: '1.10'
  compile group: 'com.squareup.okio', name: 'okio', version: '1.13.0'
  compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml',
          version: '2.9.1'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.1'
  compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names',
          version: '2.9.1'
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.1'
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.1'
  compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor',
          version: '2.9.1'
  compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
  compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
  compile group: 'commons-io', name: 'commons-io', version: '2.5'
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
  compile group: 'com.google.guava', name: 'guava', version: '23.0'
  compile group: 'com.jakewharton.byteunits', name: 'byteunits', version: '0.9.1'

  // JUnit Jupiter API and TestEngine implementation
  testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
  testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

  // If you also want to support JUnit 3 and JUnit 4 tests
  testCompile("junit:junit:${junit4Version}")
  testRuntime("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}")

  // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
  testRuntime("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")

  testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

  testRuntime group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.25'
}

junitPlatform {
}

//packaging tests
task packageTests(type: Jar) {
  from sourceSets.test.output
  classifier = 'tests'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar, packageTests, jar
}

// TODO support publish to maven local

if (project.hasProperty('ossrhUser')) {
  signing {
    sign configurations.archives
  }

  nexusStaging {
    username = ossrhUser
    password = ossrhPassword
//  packageGroup = "org.mycompany.myproject" //optional if packageGroup == project.getGroup()
//  stagingProfileId = "yourStagingProfileId" //when not defined will be got from server using "packageGroup"
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUser, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUser, password: ossrhPassword)
        }

        pom.project {
          name project.name
          group 'com.baulsupp'
          description "Command Line Output Library"
          url "https://github.com/yschimke/oksocial-output"

          scm {
            connection 'scm:git:https://github.com/yschimke/oksocial-output.git'
            developerConnection 'scm:git:git@github.com:yschimke/oksocial-output.git'
            url 'https://github.com/yschimke/oksocial-output.git'
          }

          licenses {
            license {
              name 'Apache License'
              url 'http://opensource.org/licenses/apache-2.0'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id = 'yschimke'
              name = 'Yuri Schimke'
              email = 'yuri@schimke.ee'
            }
          }

          packaging 'jar'
        }
      }
    }
  }
}

buildScan { licenseAgreementUrl = 'https://gradle.com/terms-of-service'; licenseAgree = 'yes' }
